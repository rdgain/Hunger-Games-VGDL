BasicGame no_players=2 square_size=30

    SpriteSet
        land > Immovable hidden=True,True img=oryx/grass autotiling=True

        #-----------------------------------------------------------------------------------------------
        #fog of war stuff

        fogofwar > Immovable color=DARKGRAY
            fogA > invisible=False,True hidden=False,True #only A sees fog, B doesn't
            fogB > invisible=True,False hidden=True,False #only B sees fog, A doesn't
            noFog > invisible=True hidden=True,True #no one sees fog
            fog > invisible=False hidden=False,False #everyone sees fog

        removeFog >
            removeFogA > color=RED invisible=True hidden=True,True
                rmFogA > Spreader stype=rmA1 singleton=True
                rmA1 > Spreader stype=rmA2
                rmA2 > Flicker

            removeFogB > color=BLUE invisible=True hidden=True,True
                rmFogB > Spreader stype=rmB1 singleton=True
                rmB1 > Spreader stype=rmB2
                rmB2 > Flicker

        #end of fog of war stuff
        #---------------------------------------------------------------------------------------------

        berryBush > SpawnPoint total=100 prob=0.001 img=oryx/bush5 shrinkfactor=0.7
            berryBushVisA > hidden=False,True invisible=False,True stype=berryVisA
            berryBushVisB > hidden=True,False invisible=True,False stype=berryVisB
            berryBushVis > hidden=False invisible=False stype=berryVis
            berryBushHid > hidden=True invisible=True stype=berryHid

        resourcepack > Resource #these die after a number of things spawned
            berry > img=oryx/bush3 shrinkfactor=0.7
                berryVisA > hidden=False,True invisible=False,True
                berryVisB > hidden=True,False invisible=True,False
                berryVis > hidden=False invisible=False
                berryHid > hidden=True invisible=True

        flyfood > Resource color=PINK shrinkfactor=0.5 img=oryx/mushroom2
        food > Resource color=BLUE shrinkfactor=0.5 img=oryx/mushroom1

        #-----------------------------------------------------------------------------------------------
        #avatar stuff

        avatar > ShootAvatar speed=1 healthPoints=200 limitHealthPoints=200
            avatarA > img=newset/girl2 frameRate=8 color=RED  #always visible to himself
                avVisA > hidden=False,False invisible=False stype=hitAvis     #visible to B too
                avHidA > hidden=False,True invisible=False,True stype=hitAhid #hidden from B
            avatarB > img=newset/man3 frameRate=8 color=BLUE  #always visible to himself
                avVisB > hidden=False,False invisible=False stype=hitBvis     #visible to A too
                avHidB > hidden=True,False invisible=True,False stype=hitBhid #hidden from A
        hit > Flicker img=oryx/staff1 shrinkfactor=0.5
            hitA > singleton=True
                hitAvis > hidden=False invisible=False
                hitAhid > hidden=False,True invisible=False,True
            hitB > singleton=True
                hitBvis > hidden=False invisible=False
                hitBhid > hidden=True,False invisible=True,False

        #end of avatar stuff
        #---------------------------------------------------------------------------------------------

        animalSpawn > SpawnPoint stype=bearHid prob=0.0008 invisible=True hidden=True
        flyingSpawn > SpawnPoint stype=butHid prob=0.002 invisible=True hidden=True

        animal > RandomNPC cons=2
            bear > img=oryx/bear1 cooldown=8 speed=0.3 shrinkfactor=0.4
                bearVisA > hidden=False,True invisible=False,True
                bearVisB > hidden=True,False invisible=True,False
                bearVis > hidden=False invisible=False
                bearHid > hidden=True invisible=True

        tree > Immovable color=GREEN img=oryx/tree2
            treeVisA > hidden=False,True invisible=False,True
            treeVisB > hidden=True,False invisible=True,False
            treeVis > hidden=False invisible=False
            treeHid > hidden=True invisible=True
        wall > Immovable color=BLACK img=oryx/wall3 autotiling=true
            wallVisA > hidden=False,True invisible=False,True
            wallVisB > hidden=True,False invisible=True,False
            wallVis > hidden=False invisible=False
            wallHid > hidden=True invisible=True

        flying > RandomNPC cons=5
            butterfly > img=newset/butterfly1 frameRate=8 speed=0.8 cooldown=2
                butVisA > hidden=False,True invisible=False,True
                butVisB > hidden=True,False invisible=True,False
                butVis > hidden=False invisible=False
                butHid > hidden=True invisible=True


        #NOTES:
        #   > give scores!

    InteractionSet

        avatarA TIME > subtractHealthPoints timer=5 repeating=True
        avatarB TIME > subtractHealthPoints timer=5 repeating=True

        #------fog-of-war--------
        fog removeFogA > transformTo stype=fogB
        fog removeFogB > transformTo stype=fogA
        fogA removeFogA > transformTo stype=noFog
        fogB removeFogB > transformTo stype=noFog

        avatarA fog fogA > spawn stype=rmFogA
        avatarB fog fogB > spawn stype=rmFogB

        avVisA fog fogB > transformTo stype=avHidA #keepHealthPoints=True
        avVisB fog fogA > transformTo stype=avHidB #keepHealthPoints=True
        avHidA noFog > transformTo stype=avVisA #keepHealthPoints=True
        avHidB noFog > transformTo stype=avVisB #keepHealthPoints=True

        treeHid fogA > transformTo stype=treeVisB
        treeHid fogB > transformTo stype=treeVisA
        treeVisA noFog > transformTo stype=treeVis
        treeVisB noFog > transformTo stype=treeVis

        wallHid fogA > transformTo stype=wallVisB
        wallHid fogB > transformTo stype=wallVisA
        wallVisA noFog > transformTo stype=wallVis
        wallVisB noFog > transformTo stype=wallVis

        berryBushHid fogB > transformTo stype=berryBushVisA
        berryBushHid fogA > transformTo stype=berryBushVisB
        berryBushVisA noFog > transformTo stype=berryBushVis
        berryBushVisB noFog > transformTo stype=berryBushVis

        butVisA noFog > transformTo stype=butVis
        butVisB noFog > transformTo stype=butVis
        butHid fogA > transformTo stype=butVisB
        butHid fogB > transformTo stype=butVisA
        butVis fogA > transformTo stype=butVisA
        butVis fogB > transformTo stype=butVisB
        butVisA fog > transformTo stype=butHid

        bearVisA noFog > transformTo stype=bearVis
        bearVisB noFog > transformTo stype=bearVis
        bearHid fogA > transformTo stype=bearVisB
        bearHid fogB > transformTo stype=bearVisA
        bearVis fogA > transformTo stype=bearVisA
        bearVis fogB > transformTo stype=bearVisB
        bearVisA fog > transformTo stype=bearHid

        #------collision---------
        animal animal avatar EOS wall > stepBack
        flying flying EOS > stepBack
        avatarA wall tree > stepBack stype=removeFogA
        avatarB wall tree > stepBack stype=removeFogB

        #------collecting--------
        avatar berry > addHealthPointsToMax value=5
        berry avatar > killSprite

        avatar flyfood > addHealthPointsToMax value=20
        flyfood avatar > killSprite

        avatar food > addHealthPointsToMax value=50
        food avatar > killSprite

        #-------killing----------
        avatarA hitB > killSprite
        avatarB hitA > killSprite

        flying hit > spawn stype=flyfood
        flying hitA > killBoth
        flying hitB > killBoth

        animal hit > spawn stype=food
        animal hitA > killBoth
        animal hitB > killBoth

        #-------other------------
        removeFogA removeFogA > killSprite #avoid a crazy number of sprites
    
    TerminationSet
        SpriteCounter stype=avatar limit=0 win=False,False
        SpriteCounter stype=avatarA limit=0 win=False,True
        SpriteCounter stype=avatarB limit=0 win=True,False
    
    LevelMapping
        _ > land fog
        A > avHidA land fogB
        B > avHidB land fogA
        h > treeHid land fog
        w > land wallHid fog
        b > land berryBushHid fog
        u > land flyingSpawn fog
        q > land tree animalSpawn fog

        f > arrows land fog
        g > bow land fog